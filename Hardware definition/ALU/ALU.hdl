// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Pre-setting X:
    Mux16(a=x, b[0..15]=false, sel=zx, out=x2);  // if (zx == 1) set x = 0
    Not16(in=x2, out=notX2);
    Mux16(a=x2, b=notX2, sel=nx, out=x3);        // if (nx == 1) set x = !x
    // Pre-setting Y:
    Mux16(a=y, b[0..15]=false, sel=zy, out=y2);  // if (zy == 1) set y = 0
    Not16(in=y2, out=notY2);
    Mux16(a=y2, b=notY2, sel=ny, out=y3);        // if (ny == 1) set y = !y
    // Operations:
    And16(a=x3, b=y3, out=andXY);                // if (f == 0)  set out = x & y
    Add16(a=x3, b=y3, out=addXY);                // if (f == 1)  set out = x + y
    Mux16(a=andXY, b=addXY, sel=f, out=opXY);
    // Post-setting:
    Not16(in=opXY, out=notXY); // Calculate !out // if (no == 1) set out = !out
    // if (MSB=1) -> Raise negative flag
    Mux16(a=opXY, b=notXY, sel=no, out[0..15]=preout, out[15]=negFlag, out[15]=ng, out=out);
    // Setting flags:
    Add16(a=preout, b[0..14]=true, b[15]=false, out[15]=zero); // if (out-1 gives MSB=1)
    Not(in=zero, out=zFlag);                                   // we had (MSB=0) before
    Mux(a=zFlag, b=false, sel=negFlag, out=zr);                // -> Raise zero flag
}
